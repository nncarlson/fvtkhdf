!!
!! VTKHDF_UG_FILE_TYPE
!!
!! This module defines a derived type for exporting mesh-based solution data
!! to a VTKHDF format file that can be read by the ParaView visualizaton tool.
!! The format uses HDF5 for on-disk storage.
!!
!! Neil Carlson <neil.n.carlson@gmail.com>
!! January 2026
!!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!
!! NOTES
!!
!! The up-to-date specification for VTKHDF is at
!! https://docs.vtk.org/en/latest/vtk_file_formats/vtkhdf_file_format/index.html
!!
!! This module was written for version 2.5 of the format.
!!
!! This module uses the "UnstructuredGrid" type of format. It supports static
!! and time dependent data (point and cell), but assumes a single static mesh.
!!

#include "f90_assert.fpp"

module vtkhdf_ug_file_type

  use,intrinsic :: iso_fortran_env
  use vtkhdf_h5_c_binding
  use vtkhdf_h5
  use vtkhdf_ug_type
  use vtkhdf_ctx_type
  implicit none
  private

#:set num_specs = [ {"suffix":"int32",  "decl":"integer(int32)"}, &
                  & {"suffix":"int64",  "decl":"integer(int64)"}, &
                  & {"suffix":"real32", "decl":"real(real32)"}, &
                  & {"suffix":"real64", "decl":"real(real64)"} ]
#:set locations = ["cell", "point"]
  type, public :: vtkhdf_ug_file
    private
    type(vtkhdf_ctx) :: ctx
    integer(hid_t)  :: file_id=H5I_INVALID_HID
    type(vtkhdf_ug) :: ug
  contains
    procedure :: create
    procedure :: close
    procedure :: write_mesh
    procedure :: write_time_step
#:for s in num_specs
#:for loc in locations
    generic :: write_${loc}$_data => write_${loc}$_data_${s["suffix"]}$
    procedure, private :: write_${loc}$_data_${s["suffix"]}$
    generic :: register_temporal_${loc}$_data => register_temporal_${loc}$_data_${s["suffix"]}$
    procedure, private :: register_temporal_${loc}$_data_${s["suffix"]}$
    generic :: write_temporal_${loc}$_data => write_temporal_${loc}$_data_${s["suffix"]}$
    procedure, private :: write_temporal_${loc}$_data_${s["suffix"]}$
#:endfor
#:endfor
    final :: vtkhdf_ug_file_delete
  end type

  !! TODO: get a more exhaustive list, separate module
  integer(int8), parameter, public :: VTK_TETRA = 10
  integer(int8), parameter, public :: VTK_HEXAHEDRON = 12
  integer(int8), parameter, public :: VTK_WEDGE = 13
  integer(int8), parameter, public :: VTK_PYRAMID = 14

  integer, parameter :: vtkhdf_version(*) = [2,5]

contains

  !! Finalizer for VTKHDF_UG_FILE objects. We free heap memory we own but avoid
  !! doing things that may require syncronization with other ranks (MPI/PHDF5)
  !! because where implicit finalization occurs it is not guaranteed to be
  !! collective or ordered with respect to other ranks. This can leak HDF5
  !! IDs and the MPI communicator, but that is unavoidable. Users should
  !! always use CLOSE to do a proper collective cleanup and close of the file.

  subroutine vtkhdf_ug_file_delete(this)
    use mpi, only: MPI_COMM_NULL
    type(vtkhdf_ug_file), intent(inout) :: this
    this%file_id = H5I_INVALID_HID
    this%ctx%comm = MPI_COMM_NULL
  end subroutine

  !! Cleanly closes H5 identifiers and the file, and default initializes.
  subroutine close(this)
    use mpi, only: MPI_COMM_NULL, MPI_Comm_free
    class(vtkhdf_ug_file), intent(inout) :: this
    integer :: ierr
    call this%ug%close
    if (H5Iis_valid(this%file_id) > 0) ierr = H5Fclose(this%file_id)
    if (this%ctx%comm /= MPI_COMM_NULL) call MPI_Comm_free(this%ctx%comm, ierr)
    call finalize(this) ! free local memory
  contains
    subroutine finalize(this)
      class(vtkhdf_ug_file), intent(out) :: this
    end subroutine
  end subroutine

  subroutine create(this, filename, comm, stat, errmsg, temporal)

    use,intrinsic :: iso_c_binding, only: c_bool

    class(vtkhdf_ug_file), intent(out) :: this
    character(*), intent(in) :: filename
    integer, intent(in) :: comm
    integer, intent(out) :: stat
    character(:), allocatable, intent(out) :: errmsg
    logical, intent(in), optional :: temporal

    integer(hid_t) :: fapl
    integer :: ierr

    call this%ctx%init(comm)

    call init_hdf5

    !! Open the file (read/write), overwrite any existing file
    fapl = H5Pcreate(H5P_FILE_ACCESS)
    stat = H5Pset_fapl_mpio(fapl, this%ctx%comm)
    stat = H5Pset_all_coll_metadata_ops(fapl, is_collective=.true._c_bool)
    stat = H5Pset_coll_metadata_write(fapl, is_collective=.true._c_bool)
    this%file_id = H5Fcreate(filename, H5F_ACC_TRUNC, H5P_DEFAULT, fapl)
    ierr = H5Pclose(fapl)
    if (this%ctx%global_any(this%file_id < 0)) then
      stat = 1
      errmsg = 'failed to open file'
      return
    end if

    call this%ug%init(this%file_id, 'VTKHDF', this%ctx, stat, errmsg, temporal)
    if (stat /= 0) then
      errmsg = 'error creating root VTKHDF group: ' // errmsg
      return
    end if

  end subroutine create

  !! Write the UnstructuredGrid data. The unstructured mesh is described in
  !! the conventional manner by the X, CNODE, TYPE, and XCNODE arrays. This
  !! procedure must be called before any of the following procedures.

  subroutine write_mesh(this, x, cnode, xcnode, types, stat, errmsg)
    class(vtkhdf_ug_file), intent(inout) :: this
    real(real64), intent(in) :: x(:,:)
    integer, intent(in) :: cnode(:), xcnode(:)
    integer(int8), intent(in) :: types(:)
    integer, intent(out) :: stat
    character(:), allocatable, intent(out) :: errmsg
    call this%ug%write_mesh(x, cnode, xcnode, types, stat, errmsg)
  end subroutine

  !! Writes the cell-based data ARRAY to a new named cell dataset. Scalar,
  !! vector, and tensor data are supported. For temporal files, this dataset
  !! is static and not associated with any time step.
#:for s in num_specs
#:for loc in locations

  subroutine write_${loc}$_data_${s["suffix"]}$(this, name, array, stat, errmsg)
    class(vtkhdf_ug_file), intent(in) :: this
    character(*), intent(in) :: name
    ${s["decl"]}$, intent(in) :: array(..)
    integer, intent(out) :: stat
    character(:), allocatable, intent(out) :: errmsg
    call this%ug%write_${loc}$_data_${s["suffix"]}$(name, array, stat, errmsg)
  end subroutine
#:endfor
#:endfor

  !! Register the specified NAME as a time-dependent cell dataset. This
  !! writes no data, but only configures some necessary internal metadata.
  !! The MOLD array argument shall have the same type, kind, and rank as
  !! the actual dataset, and the same extent in all but the last dimension,
  !! whose extent is ignored, but the array values themselves are not
  !! accessed. Scalar, vector, and tensor-valued mesh data are supported
  !! (rank-1, 2, and 3 MOLD).
#:for s in num_specs
#:for loc in locations

  subroutine register_temporal_${loc}$_data_${s["suffix"]}$(this, name, mold, stat, errmsg)
    class(vtkhdf_ug_file), intent(inout) :: this
    character(*), intent(in) :: name
    ${s["decl"]}$, intent(in) :: mold(..)
    integer, intent(out) :: stat
    character(:), allocatable, intent(out) :: errmsg
    call this%ug%register_temporal_${loc}$_data_${s["suffix"]}$(name, mold, stat, errmsg)
  end subroutine
#:endfor
#:endfor

  !! Mark the start of a new time step with time value TIME. Subsequent output
  !! of time-dependent datasets will be associated with this time step.

  subroutine write_time_step(this, time)
    class(vtkhdf_ug_file), intent(inout) :: this
    real(real64), intent(in) :: time
    call this%ug%write_time_step(time)
  end subroutine

  !! Write the cell-based data ARRAY to the named time-dependent cell dataset.
  !! The data is associated with the current time step.
#:for s in num_specs
#:for loc in locations

  subroutine write_temporal_${loc}$_data_${s["suffix"]}$(this, name, array, stat, errmsg)
    class(vtkhdf_ug_file), intent(in) :: this
    character(*), intent(in) :: name
    ${s["decl"]}$, intent(in) :: array(..)
    integer, intent(out) :: stat
    character(:), allocatable :: errmsg
    call this%ug%write_temporal_${loc}$_data_${s["suffix"]}$(name, array, stat, errmsg)
  end subroutine
#:endfor
#:endfor

end module vtkhdf_ug_file_type
