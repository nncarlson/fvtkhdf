!!
!! VTKHDF_UG_TYPE
!!
!! This module defines an internal derived type used to manage an HDF5 tree
!! storing an UnstructuredGrid VTKHDF dataset. It serves as a core component of
!! the VTKHDF_MB_FILE and VTKHDF_UG_file types. This implementation supports
!! static and time-dependent point and cell data, but assumes a single static
!! mesh.
!!
!! Copyright (c) 2026 Neil Carlson <neil.n.carlson@gmail.com>
!! SPDX-License-Identifier: BSD-2-Clause
!!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!
!! NOTES
!!
!! The latest specification for the VTKHDF file format is available at:
!! https://docs.vtk.org/en/latest/vtk_file_formats/vtkhdf_file_format/index.html#
!!

#include "f90_assert.fpp"

module vtkhdf_ug_type

  use,intrinsic :: iso_fortran_env
  use vtkhdf_h5_c_binding
  use vtkhdf_h5
  use vtkhdf_ctx_type
  use mpi
  implicit none
  private

#:set int_specs  = [ {"kind":"int32",  "decl":"integer(int32)"}, &
                   & {"kind":"int64",  "decl":"integer(int64)"} ]
#:set real_specs = [ {"kind":"real32", "decl":"real(real32)"}, &
                   & {"kind":"real64", "decl":"real(real64)"} ]
#:set num_specs = int_specs + real_specs
  type, public :: vtkhdf_ug
    type(vtkhdf_ctx) :: ctx ! unowned copy
    integer(hid_t) :: root_id=H5I_INVALID_HID
    integer(hid_t) :: cgrp_id=H5I_INVALID_HID, pgrp_id=H5I_INVALID_HID
    integer :: nnode, ncell, nnode_tot, ncell_tot, npart
    logical :: temporal = .false.
    integer :: nsteps = -1
    integer(hid_t) :: steps_id=H5I_INVALID_HID
    integer(hid_t) :: cogrp_id=H5I_INVALID_HID, pogrp_id=H5I_INVALID_HID
    type(temporal_data), pointer :: temporal_point_data => null()
    type(temporal_data), pointer :: temporal_cell_data => null()
  contains
    procedure :: init
    procedure :: close
#:for r in real_specs
#:for i in int_specs
    generic :: write_mesh => write_mesh_${r["kind"]}$_${i["kind"]}$
    procedure, private :: write_mesh_${r["kind"]}$_${i["kind"]}$
#:endfor
#:endfor
    procedure :: write_time_step
#:for s in num_specs
    procedure :: write_cell_data_${s["kind"]}$
    procedure :: write_point_data_${s["kind"]}$
    procedure :: register_temporal_cell_data_${s["kind"]}$
    procedure :: register_temporal_point_data_${s["kind"]}$
    procedure :: write_temporal_cell_data_${s["kind"]}$
    procedure :: write_temporal_point_data_${s["kind"]}$
#:endfor
    final :: vtkhdf_ug_delete
  end type

  type :: temporal_data
    character(:), allocatable :: name
    integer :: next_offset = 0
    logical :: flag = .false.
    type(temporal_data), pointer :: next => null()
  contains
    final :: temporal_data_delete
  end type

contains

  !! Finalizer for VTKHDF_UG_FILE objects. We free heap memory we own but avoid
  !! doing things that may require syncronization with other ranks (MPI/PHDF5)
  !! because where implicit finalization occurs it is not guaranteed to be
  !! collective or ordered with respect to other ranks. This can leak HDF5
  !! IDs, but that is unavoidable. Users should always use CLOSE to do a proper
  !! collective cleanup.

  subroutine vtkhdf_ug_delete(this)
    type(vtkhdf_ug), intent(inout) :: this
    if (associated(this%temporal_cell_data)) deallocate(this%temporal_cell_data)
    if (associated(this%temporal_point_data)) deallocate(this%temporal_point_data)
    this%cogrp_id = H5I_INVALID_HID
    this%pogrp_id = H5I_INVALID_HID
    this%steps_id = H5I_INVALID_HID
    this%cgrp_id  = H5I_INVALID_HID
    this%pgrp_id  = H5I_INVALID_HID
    this%root_id  = H5I_INVALID_HID
  end subroutine

  subroutine close(this)
    class(vtkhdf_ug), intent(inout) :: this
    integer :: ierr
    if (H5Iis_valid(this%cogrp_id) > 0) ierr = H5Gclose(this%cogrp_id)
    if (H5Iis_valid(this%pogrp_id) > 0) ierr = H5Gclose(this%pogrp_id)
    if (H5Iis_valid(this%steps_id) > 0) ierr = H5Gclose(this%steps_id)
    if (H5Iis_valid(this%cgrp_id) > 0) ierr = H5Gclose(this%cgrp_id)
    if (H5Iis_valid(this%pgrp_id) > 0) ierr = H5Gclose(this%pgrp_id)
    if (H5Iis_valid(this%root_id) > 0) ierr = H5Gclose(this%root_id)
    call finalization(this) ! free local memory
  contains
    subroutine finalization(this)
      class(vtkhdf_ug), intent(out) :: this
    end subroutine
  end subroutine

  recursive subroutine temporal_data_delete(this)
    type(temporal_data), intent(inout) :: this
    if (associated(this%next)) deallocate(this%next)
  end subroutine

  !! Create a new VTKHDF UnstructuredGrid type group. To create a group
  !! supporting time-dependent data, specify the optional argument
  !! TEMPORAL to true; otherwise a static group is created by default.

  subroutine init(this, loc_id, name, ctx, stat, errmsg, temporal)

    use vtkhdf_version_param

    class(vtkhdf_ug), intent(inout) :: this
    integer(hid_t), intent(in) :: loc_id
    character(*), intent(in) :: name
    type(vtkhdf_ctx), intent(in) :: ctx
    integer, intent(out) :: stat
    character(:), allocatable, intent(out) :: errmsg
    logical, intent(in), optional :: temporal

    integer :: ierr

    this%ctx = ctx

    this%root_id = H5Gcreate(loc_id, name)
    if (this%ctx%global_any(this%root_id < 0)) then
      stat = 1
      errmsg = 'unable to create group'
      return
    end if

    call h5_write_attr(this%ctx, this%root_id, 'Version', vtkhdf_version, stat, errmsg)
    if (stat /= 0) return
    call h5_write_attr(this%ctx, this%root_id, 'Type', 'UnstructuredGrid', stat, errmsg)
    if (stat /= 0) return

    this%cgrp_id = H5Gcreate(this%root_id, 'CellData')
    if (this%ctx%global_any(this%cgrp_id < 0)) then
      stat = 1
      errmsg = 'unable to create "CellData" group'
      return
    end if

    this%pgrp_id = H5Gcreate(this%root_id, 'PointData')
    if (this%ctx%global_any(this%pgrp_id < 0)) then
      stat = 1
      errmsg = 'unable to create "PointData" group'
      return
    end if

    if (present(temporal)) this%temporal = temporal
    if (this%temporal) then

      this%steps_id = H5Gcreate(this%root_id, 'Steps')
      if (this%ctx%global_any(this%steps_id < 0)) then
        stat = 1
        errmsg = 'unable to create "Steps" group'
        return
      end if

      this%cogrp_id = H5Gcreate(this%steps_id, 'CellDataOffsets')
      if (this%ctx%global_any(this%cogrp_id < 0)) then
        stat = 1
        errmsg = 'unable to create "CellDataOffsets" group'
        return
      end if

      this%pogrp_id = H5Gcreate(this%steps_id, 'PointDataOffsets')
      if (this%ctx%global_any(this%pogrp_id < 0)) then
        stat = 1
        errmsg = 'unable to create "PointDataOffsets" group'
        return
      end if

      this%nsteps = 0
      call h5_write_attr(this%ctx, this%steps_id, 'NSteps', this%nsteps, stat, errmsg)
      if (stat /= 0) return

      associate (rmold => [1.0_real64], chunk_size => 100)
        call h5_create_unlimited_dataset(this%ctx, this%steps_id, 'Values', rmold, chunk_size, stat, errmsg)
        if (stat /= 0) return
      end associate

      ! NB: Only static meshes are supported. ParaView 6.1 uses this attribute
      ! setting to signal it should use mesh caching -- read once and reuse.
      call h5_write_attr(this%ctx, this%steps_id, 'StaticMesh', 1, stat, errmsg)
      if (stat /= 0) return

      ! NB: These are no longer needed in ParaView 6.1 when StaticMesh=1, but
      ! we retain them for compatibility with earlier versions. They will be
      ! filled with 0 offsets and the constant number of parts.
      associate (imold => [1], chunk_size => 100)
        call h5_create_unlimited_dataset(this%ctx, this%steps_id, 'PointOffsets', imold, chunk_size, stat, errmsg)
        if (stat /= 0) return
        call h5_create_unlimited_dataset(this%ctx, this%steps_id, 'CellOffsets', imold, chunk_size, stat, errmsg)
        if (stat /= 0) return
        call h5_create_unlimited_dataset(this%ctx, this%steps_id, 'ConnectivityIdOffsets', imold, chunk_size, stat, errmsg)
        if (stat /= 0) return
        call h5_create_unlimited_dataset(this%ctx, this%steps_id, 'NumberOfParts', imold, chunk_size, stat, errmsg)
        if (stat /= 0) return
        call h5_create_unlimited_dataset(this%ctx, this%steps_id, 'PartOffsets', imold, chunk_size, stat, errmsg)
        if (stat /= 0) return
      end associate

    end if

    stat = 0

  end subroutine init

  !! Write the unstructured mesh to the file. The mesh is described in the
  !! conventional manner by the X, CNODE, and XCNODE arrays. The additional
  !! array TYPES provides an unambiguous specification of the cell types,
  !! that we would otherwise infer from the OFFSETS array. This procedure
  !! must be called before any of the following procedures.

  !! NB: Paraview and/or the VTKHDF reader currently has a problem handling
  !! 0-sized parts (see https://gitlab.kitware.com/vtk/vtk/-/issues/19923).
  !! As a workaround, we omit any empty part by not contributing anything
  !! at all to the NumberOf* and Offsets datasets instead of the 0 we
  !! normally would. Other datasets like Connectivity already contribute
  !! nothing for empty parts.
#:for r in real_specs
#:for i in int_specs

  subroutine write_mesh_${r["kind"]}$_${i["kind"]}$(this, x, cnode, xcnode, types, stat, errmsg)

    class(vtkhdf_ug), intent(inout) :: this
    ${r["decl"]}$, intent(in) :: x(:,:)
    ${i["decl"]}$, intent(in) :: cnode(:), xcnode(:)
    integer(int8), intent(in) :: types(:)
    integer, intent(out) :: stat
    character(:), allocatable, intent(out) :: errmsg

    integer :: ierr, idum(0)

    INSIST(this%root_id > 0)

    this%nnode = size(x, dim=2)
    this%ncell = size(types)

    ASSERT(size(xcnode) == this%ncell+1)
    ASSERT(size(cnode) == xcnode(size(xcnode))-1)
    ASSERT(minval(cnode) >= 1)
    ASSERT(maxval(cnode) <= this%nnode)

    call this%ctx%global_sum(this%nnode, this%nnode_tot)
    call this%ctx%global_sum(this%ncell, this%ncell_tot)
    call this%ctx%global_sum(merge(1, 0, this%ncell>0), this%npart) ! see NB above

    if (this%ncell > 0) then
      call h5_write_dataset(this%ctx, this%root_id, 'NumberOfPoints', this%nnode, stat, errmsg)
    else ! see NB above
      call h5_write_dataset(this%ctx, this%root_id, 'NumberOfPoints', idum, stat, errmsg)
    end if
    if (stat /= 0) return

    if (this%ncell > 0) then
      call h5_write_dataset(this%ctx, this%root_id, 'NumberOfCells',  this%ncell, stat, errmsg)
    else ! see NB above
      call h5_write_dataset(this%ctx, this%root_id, 'NumberOfCells',  idum, stat, errmsg)
    end if
    if (stat /= 0) return

    if (this%ncell > 0) then
      call h5_write_dataset(this%ctx, this%root_id, 'NumberOfConnectivityIds', size(cnode), stat, errmsg)
    else ! see NB above
      call h5_write_dataset(this%ctx, this%root_id, 'NumberOfConnectivityIds', idum, stat, errmsg)
    end if
    if (stat /= 0) return

    if (this%ncell > 0) then
      call h5_write_dataset(this%ctx, this%root_id, 'Offsets', xcnode-1, stat, errmsg) ! offsets instead of starting indices
    else ! see NB above
      call h5_write_dataset(this%ctx, this%root_id, 'Offsets', [${i["decl"]}$::], stat, errmsg)
    end if
    if (stat /= 0) return

    if (this%ncell > 0) then
      call h5_write_dataset(this%ctx, this%root_id, 'Connectivity', cnode-1, stat, errmsg)  ! 0-based indexing
    else ! workaround for gfortran bug https://gcc.gnu.org/bugzilla/show_bug.cgi?id=123899
      call h5_write_dataset(this%ctx, this%root_id, 'Connectivity', [${i["decl"]}$::], stat, errmsg)
    endif
    if (stat /= 0) return

    call h5_write_dataset(this%ctx, this%root_id, 'Types', types, stat, errmsg)
    if (stat /= 0) return

    call h5_write_dataset(this%ctx, this%root_id, 'Points', x, stat, errmsg)
    if (stat /= 0) return

  end subroutine

#:endfor
#:endfor
  !! Writes the cell DATA to a new CellData group dataset NAME. Scalar,
  !! vector, and tensor cell-based data are supported. In the case of a
  !! temporal file supporting time-dependent datasets, this dataset is
  !! static and not associated with any time step.
#:for s in num_specs

  subroutine write_cell_data_${s["kind"]}$(this, name, data, stat, errmsg)
    class(vtkhdf_ug), intent(in) :: this
    character(*), intent(in) :: name
    ${s["decl"]}$, intent(in) :: data(..)
    integer, intent(out) :: stat
    character(:), allocatable, intent(out) :: errmsg
    integer, allocatable :: dims(:)
    dims = shape(data)
    INSIST(size(dims) >= 1 .and. size(dims) <= 3)
    INSIST(dims(size(dims)) == this%ncell)
    call h5_write_dataset(this%ctx, this%cgrp_id, name, data, stat, errmsg)
  end subroutine
#:endfor

  !! Writes the point DATA to a new PointData group dataset NAME. Scalar,
  !! vector, and tensor point-based data are supported. In the case of a
  !! temporal file supporting time-dependent datasets, this dataset is
  !! static and not associated with any time step.
#:for s in num_specs

  subroutine write_point_data_${s["kind"]}$(this, name, data, stat, errmsg)
    class(vtkhdf_ug), intent(in) :: this
    character(*), intent(in) :: name
    ${s["decl"]}$, intent(in) :: data(..)
    integer, intent(out) :: stat
    character(:), allocatable, intent(out) :: errmsg
    integer, allocatable :: dims(:)
    dims = shape(data)
    INSIST(size(dims) >= 1 .and. size(dims) <= 3)
    INSIST(dims(size(dims)) == this%nnode)
    call h5_write_dataset(this%ctx, this%pgrp_id, name, data, stat, errmsg)
  end subroutine
#:endfor

  !! Register the specified NAME as a time-dependent CellData group dataset.
  !! This writes no data, but only configures some necessary metadata.
  !! The MOLD array argument shall have the same type, kind, and rank as the
  !! actual cell data, and the same extent in all but the last dimension,
  !! but the array values themselves are not accessed. Scalar, vector, and
  !! tensor-valued cell data are supported (rank-1, 2, and 3 MOLD).
#:for s in num_specs

  subroutine register_temporal_cell_data_${s["kind"]}$(this, name, mold, stat, errmsg)

    class(vtkhdf_ug), intent(inout) :: this
    character(*), intent(in) :: name
    ${s["decl"]}$, intent(in) :: mold(..)
    integer, intent(out) :: stat
    character(:), allocatable, intent(out) :: errmsg

    type(temporal_data), pointer :: new

    INSIST(this%nsteps == 0)

    !! Dataset for the cell data
    associate (chunk_size => this%ncell_tot)
      call h5_create_unlimited_dataset(this%ctx, this%cgrp_id, name, mold, chunk_size, stat, errmsg)
    end associate
    if (stat /= 0) return

    !! Dataset for its time step offsets
    associate (mold => [1], chunk_size => 100)
      call h5_create_unlimited_dataset(this%ctx, this%cogrp_id, name, mold, chunk_size, stat, errmsg)
    end associate
    if (stat /= 0) return

    !! Add to list CellData datasets
    allocate(new)
    new%name = name
    new%next => this%temporal_cell_data
    this%temporal_cell_data => new

  end subroutine register_temporal_cell_data_${s["kind"]}$
#:endfor

  !! Register the specified NAME as a time-dependent PointData group dataset.
  !! This writes no data, but only configures some necessary internal metadata.
  !! The MOLD array argument shall have the same type, kind, and rank as the
  !! actual point data, and the same extent in all but the last dimension,
  !! but the array values themselves are not accessed. Scalar, vector, and
  !! tensor-valued point data are supported (rank-1, 2, and 3 MOLD).
#:for s in num_specs

  subroutine register_temporal_point_data_${s["kind"]}$(this, name, mold, stat, errmsg)

    class(vtkhdf_ug), intent(inout) :: this
    character(*), intent(in) :: name
    ${s["decl"]}$, intent(in) :: mold(..)
    integer, intent(out) :: stat
    character(:), allocatable, intent(out) :: errmsg

    type(temporal_data), pointer :: new

    INSIST(this%nsteps == 0)

    !! Dataset for the point data
    associate (chunk_size => this%nnode_tot)
      call h5_create_unlimited_dataset(this%ctx, this%pgrp_id, name, mold, chunk_size, stat, errmsg)
    end associate
    if (stat /= 0) return

    !! Dataset for its time step offsets
    associate (mold => [1], chunk_size => 100)
      call h5_create_unlimited_dataset(this%ctx, this%pogrp_id, name, mold, chunk_size, stat, errmsg)
    end associate
    if (stat /= 0) return

    !! Add to list PointData datasets
    allocate(new)
    new%name = name
    new%next => this%temporal_point_data
    this%temporal_point_data => new

  end subroutine register_temporal_point_data_${s["kind"]}$
#:endfor

  !! Mark the start of a new time step with time value TIME. Subsequent output
  !! of time-dependent datasets will be associated with this time step.

  subroutine write_time_step(this, time)

    class(vtkhdf_ug), intent(inout) :: this
    real(real64), intent(in) :: time

    type(temporal_data), pointer :: tmp
    integer :: stat
    character(:), allocatable :: errmsg

    INSIST(this%nsteps >= 0)

    this%nsteps = this%nsteps + 1
    call h5_write_attr(this%ctx, this%steps_id, 'NSteps', this%nsteps, stat, errmsg)
    INSIST(stat == 0)

    call h5_append_to_dataset(this%ctx, this%steps_id, 'Values', time, stat, errmsg, root=0)
    INSIST(stat == 0)

    !! A single mesh is used for all time steps so there are no offsets
    call h5_append_to_dataset(this%ctx, this%steps_id, 'PointOffsets', 0, stat, errmsg, root=0)
    INSIST(stat == 0)
    call h5_append_to_dataset(this%ctx, this%steps_id, 'CellOffsets', 0, stat, errmsg, root=0)
    INSIST(stat == 0)
    call h5_append_to_dataset(this%ctx, this%steps_id, 'ConnectivityIdOffsets', 0, stat, errmsg, root=0)
    INSIST(stat == 0)
    call h5_append_to_dataset(this%ctx, this%steps_id, 'NumberOfParts', this%npart, stat, errmsg, root=0)
    INSIST(stat == 0)
    call h5_append_to_dataset(this%ctx, this%steps_id, 'PartOffsets', 0, stat, errmsg, root=0)
    INSIST(stat == 0)

    !! Cell and point data offsets for each of the temporal datasets for this timestep

    tmp => this%temporal_point_data
    do while (associated(tmp))
      tmp%flag = .false.  ! dataset not yet written for this time step
      call h5_append_to_dataset(this%ctx, this%pogrp_id, tmp%name, tmp%next_offset, stat, errmsg, root=0)
      INSIST(stat == 0)
      tmp => tmp%next
    end do

    tmp => this%temporal_cell_data
    do while (associated(tmp))
      tmp%flag = .false.  ! dataset not yet written for this time step
      call h5_append_to_dataset(this%ctx, this%cogrp_id, tmp%name, tmp%next_offset, stat, errmsg, root=0)
      INSIST(stat == 0)
      tmp => tmp%next
    end do

  end subroutine write_time_step

  !! Write the cell DATA to the time-dependent CellData group dataset NAME.
  !! The data is associated with the current time step. The type and shape
  !! of DATA must be consistent with the MOLD argument passed to the call
  !! of REGISTER_TEMPORAL_CELL_DATA that created this dataset.
#:for s in num_specs

  subroutine write_temporal_cell_data_${s["kind"]}$(this, name, data, stat, errmsg)

    class(vtkhdf_ug), intent(in) :: this
    character(*), intent(in) :: name
    ${s["decl"]}$, intent(in) :: data(..)
    integer, intent(out) :: stat
    character(:), allocatable :: errmsg

    integer, allocatable :: dims(:)
    type(temporal_data), pointer :: dset

    INSIST(this%nsteps >= 0)

    dims = shape(data)
    INSIST(size(dims) >= 1 .and. size(dims) <= 3)
    INSIST(dims(size(dims)) == this%ncell)

    dset => this%temporal_cell_data
    do while (associated(dset))
      if (dset%name == name) exit
      dset => dset%next
    end do

    if (associated(dset)) then
      if (dset%flag) then
        stat = 1
        errmsg = 'dataset "' // name // '" already written for this time step'
        return
      else
        call h5_append_to_dataset(this%ctx, this%cgrp_id, name, data, stat, errmsg)
        if (stat /= 0) return
        dset%next_offset = dset%next_offset + this%ncell_tot
        dset%flag = .true. ! dataset has been written for this time step
      end if
    else
      stat = 1
      errmsg = '"' // name // '" is not a valid temporal cell dataset'
      return
    end if

  end subroutine write_temporal_cell_data_${s["kind"]}$
#:endfor

  !! Write the point DATA to the time-dependent PointData group dataset NAME.
  !! The data is associated with the current time step. The type and shape
  !! of DATA must be consistent with the MOLD argument passed to the call
  !! of REGISTER_TEMPORAL_POINT_DATA that created this dataset.
#:for s in num_specs

  subroutine write_temporal_point_data_${s["kind"]}$(this, name, data, stat, errmsg)

    class(vtkhdf_ug), intent(in) :: this
    character(*), intent(in) :: name
    ${s["decl"]}$, intent(in) :: data(..)
    integer, intent(out) :: stat
    character(:), allocatable :: errmsg

    integer, allocatable :: dims(:)
    type(temporal_data), pointer :: dset

    INSIST(this%nsteps >= 0)

    dims = shape(data)
    INSIST(size(dims) >= 1 .and. size(dims) <= 3)
    INSIST(dims(size(dims)) == this%nnode)

    dset => this%temporal_point_data
    do while (associated(dset))
      if (dset%name == name) exit
      dset => dset%next
    end do

    if (associated(dset)) then
      if (dset%flag) then
        stat = 1
        errmsg = 'dataset "' // name // '" already written for this time step'
        return
      else
        call h5_append_to_dataset(this%ctx, this%pgrp_id, name, data, stat, errmsg)
        if (stat /= 0) return
        dset%next_offset = dset%next_offset + this%nnode_tot
        dset%flag = .true. ! dataset has been written for this time step
      end if
    else
      stat = 1
      errmsg = '"' // name // '" is not a valid temporal point dataset'
      return
    end if

  end subroutine write_temporal_point_data_${s["kind"]}$
#:endfor

end module vtkhdf_ug_type
