!!
!! VTKHDF_UG_TYPE
!!
!! This module defines an internal derived type used to manage an HDF5 tree
!! storing an UnstructuredGrid VTKHDF dataset. It serves as a core component of
!! the VTKHDF_MB_FILE and VTKHDF_UG_file types. This implementation supports
!! static and time-dependent point and cell data, but assumes a single static
!! mesh.
!!
!! Copyright (c) 2026 Neil Carlson <neil.n.carlson@gmail.com>
!! SPDX-License-Identifier: BSD-2-Clause
!!
!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
!!
!! NOTES
!!
!! The latest specification for the VTKHDF file format is available at:
!! https://docs.vtk.org/en/latest/vtk_file_formats/vtkhdf_file_format/index.html#
!!

#include "vtkhdf_assert.inc"

#:set int_specs  = [ {"kind":"int32",  "decl":"integer(int32)"}, &
                   & {"kind":"int64",  "decl":"integer(int64)"} ]
#:set real_specs = [ {"kind":"real32", "decl":"real(real32)"}, &
                   & {"kind":"real64", "decl":"real(real64)"} ]
#:set num_specs = int_specs + real_specs
#:set locations = ["cell", "point"]

module vtkhdf_ug_type

  use,intrinsic :: iso_fortran_env
  use vtkhdf_h5_c_binding
  use vtkhdf_h5
  use vtkhdf_ctx_type
  use vtkhdf_h5e_context_type
  implicit none
  private

  type, public :: vtkhdf_ug
    type(vtkhdf_ctx) :: ctx ! unowned copy
    integer(hid_t) :: root_id=H5I_INVALID_HID
    integer(hid_t) :: cgrp_id=H5I_INVALID_HID, pgrp_id=H5I_INVALID_HID
    integer(int64) :: nnode, ncell, nnode_tot, ncell_tot
    integer :: npart
    logical :: is_temporal = .false.
    integer :: nsteps = -1
    integer(hid_t) :: steps_id=H5I_INVALID_HID
    integer(hid_t) :: cogrp_id=H5I_INVALID_HID, pogrp_id=H5I_INVALID_HID
    type(temporal_data), pointer :: temporal_point_data => null()
    type(temporal_data), pointer :: temporal_cell_data => null()
  contains
    procedure :: init
    procedure :: close
#:for r in real_specs
#:for i in int_specs
    generic :: write_mesh => write_mesh_${r["kind"]}$_${i["kind"]}$
    procedure, private :: write_mesh_${r["kind"]}$_${i["kind"]}$
#:endfor
#:endfor
    procedure :: write_time_step
#:for s in num_specs
#:for loc in locations
    generic :: write_${loc}$_data => write_${loc}$_data_${s["kind"]}$
    procedure, private :: write_${loc}$_data_${s["kind"]}$
    generic :: register_temporal_${loc}$_data => register_temporal_${loc}$_data_${s["kind"]}$
    procedure, private :: register_temporal_${loc}$_data_${s["kind"]}$
    generic :: write_temporal_${loc}$_data => write_temporal_${loc}$_data_${s["kind"]}$
    procedure, private :: write_temporal_${loc}$_data_${s["kind"]}$
#:endfor
#:endfor
    final :: vtkhdf_ug_delete
  end type

  type :: temporal_data
    character(:), allocatable :: name
    integer(int64) :: next_offset = 0
    logical :: flag = .false.
    type(temporal_data), pointer :: next => null()
  contains
    final :: temporal_data_delete
  end type

contains

  !! Finalizer for VTKHDF_UG_FILE objects. We free heap memory we own but avoid
  !! doing things that may require syncronization with other ranks (MPI/PHDF5)
  !! because implicit finalization occurs it is not guaranteed to be collective
  !! or ordered with respect to other ranks when it occurs. This can leak HDF5
  !! IDs, but that is unavoidable. Users should always use CLOSE to do a proper
  !! collective cleanup.

  subroutine vtkhdf_ug_delete(this)
    type(vtkhdf_ug), intent(inout) :: this
    if (associated(this%temporal_cell_data)) deallocate(this%temporal_cell_data)
    if (associated(this%temporal_point_data)) deallocate(this%temporal_point_data)
    this%cogrp_id = H5I_INVALID_HID
    this%pogrp_id = H5I_INVALID_HID
    this%steps_id = H5I_INVALID_HID
    this%cgrp_id  = H5I_INVALID_HID
    this%pgrp_id  = H5I_INVALID_HID
    this%root_id  = H5I_INVALID_HID
  end subroutine

  subroutine close(this)
    class(vtkhdf_ug), intent(inout) :: this
    integer :: ierr
    if (H5Iis_valid(this%cogrp_id) > 0) ierr = H5Gclose(this%cogrp_id)
    if (H5Iis_valid(this%pogrp_id) > 0) ierr = H5Gclose(this%pogrp_id)
    if (H5Iis_valid(this%steps_id) > 0) ierr = H5Gclose(this%steps_id)
    if (H5Iis_valid(this%cgrp_id) > 0) ierr = H5Gclose(this%cgrp_id)
    if (H5Iis_valid(this%pgrp_id) > 0) ierr = H5Gclose(this%pgrp_id)
    if (H5Iis_valid(this%root_id) > 0) ierr = H5Gclose(this%root_id)
    call finalization(this) ! free local memory
  contains
    subroutine finalization(this)
      class(vtkhdf_ug), intent(out) :: this
    end subroutine
  end subroutine

  recursive subroutine temporal_data_delete(this)
    type(temporal_data), intent(inout) :: this
    if (associated(this%next)) deallocate(this%next)
  end subroutine

  !! Create a new VTKHDF UnstructuredGrid type group. To create a group
  !! supporting time-dependent data, specify the optional argument
  !! TEMPORAL to true; otherwise a static group is created by default.

  subroutine init(this, loc_id, name, ctx, temporal)

    use vtkhdf_version_param

    class(vtkhdf_ug), intent(inout) :: this
    integer(hid_t), intent(in) :: loc_id
    character(*), intent(in) :: name
    type(vtkhdf_ctx), intent(in) :: ctx
    logical, intent(in), optional :: temporal

    integer :: ierr

    this%ctx = ctx

    INSIST(this%ctx%global_all(loc_id >= 0))

    this%root_id = H5Gcreate(loc_id, name)
    INSIST(this%ctx%global_all(this%root_id >= 0))

    call h5_write_attr(this%ctx, this%root_id, 'Version', vtkhdf_version)
    call h5_write_attr(this%ctx, this%root_id, 'Type', 'UnstructuredGrid')

    this%cgrp_id = H5Gcreate(this%root_id, 'CellData')
    INSIST(this%ctx%global_all(this%cgrp_id >= 0))

    this%pgrp_id = H5Gcreate(this%root_id, 'PointData')
    INSIST(this%ctx%global_all(this%pgrp_id >= 0))

    if (present(temporal)) this%is_temporal = temporal
    if (this%is_temporal) then
      this%steps_id = H5Gcreate(this%root_id, 'Steps')
      INSIST(this%ctx%global_all(this%steps_id >= 0))

      this%cogrp_id = H5Gcreate(this%steps_id, 'CellDataOffsets')
      INSIST(this%ctx%global_all(this%cogrp_id >= 0))

      this%pogrp_id = H5Gcreate(this%steps_id, 'PointDataOffsets')
      INSIST(this%ctx%global_all(this%pogrp_id >= 0))

      this%nsteps = 0
      call h5_write_attr(this%ctx, this%steps_id, 'NSteps', this%nsteps)

      associate (mold => 1.0_real64, chunk_size => 100)
        call h5_create_unlimited_dataset(this%ctx, this%steps_id, 'Values', mold, chunk_size)
      end associate

      ! NB: Only static meshes are supported. ParaView 6.1 uses this attribute
      ! setting to signal it should use mesh caching -- read once and reuse.
      call h5_write_attr(this%ctx, this%steps_id, 'StaticMesh', 1)

      ! NB: These are no longer needed in ParaView 6.1 when StaticMesh=1, but
      ! we retain them for compatibility with earlier versions. They will be
      ! filled with 0 offsets and the constant number of parts.
      associate (mold => 0_int64, chunk_size => 100)
        call h5_create_unlimited_dataset(this%ctx, this%steps_id, 'PointOffsets', mold, chunk_size)
        call h5_create_unlimited_dataset(this%ctx, this%steps_id, 'CellOffsets', mold, chunk_size)
        call h5_create_unlimited_dataset(this%ctx, this%steps_id, 'ConnectivityIdOffsets', mold, chunk_size)
        call h5_create_unlimited_dataset(this%ctx, this%steps_id, 'PartOffsets', mold, chunk_size)
      end associate
      associate (mold => 0, chunk_size => 100)
        call h5_create_unlimited_dataset(this%ctx, this%steps_id, 'NumberOfParts', mold, chunk_size)
      end associate
    end if

  end subroutine init

  !! Write the unstructured mesh to the file. The mesh is described in the
  !! conventional manner by the X, CNODE, and XCNODE arrays. The additional
  !! array TYPES specifies the VTK cell types. This procedure must be called
  !! before any of the following procedures.

  !! NB: Paraview and/or the VTKHDF reader currently has a problem handling
  !! 0-sized parts (see https://gitlab.kitware.com/vtk/vtk/-/issues/19923).
  !! As a workaround, we omit any empty part by not contributing anything
  !! at all to the NumberOf* and Offsets datasets instead of the 0 we
  !! normally would. Other datasets like Connectivity already contribute
  !! nothing for empty parts.
#:for r in real_specs
#:for i in int_specs

  subroutine write_mesh_${r["kind"]}$_${i["kind"]}$(this, x, cnode, xcnode, types)

    class(vtkhdf_ug), intent(inout) :: this
    ${r["decl"]}$, intent(in) :: x(:,:)
    ${i["decl"]}$, intent(in) :: cnode(:), xcnode(:)
    integer(int8), intent(in) :: types(:)

    integer :: ierr
    integer(int64) :: idum(0)

    INSIST(this%root_id >= 0)

    this%nnode = size(x, dim=2, kind=int64)
    this%ncell = size(types, kind=int64)

    INSIST(size(xcnode, kind=int64) == this%ncell+1)
    INSIST(size(cnode, kind=int64) == xcnode(this%ncell+1)-1)
    INSIST(minval(cnode) >= 1)
    INSIST(maxval(cnode) <= this%nnode)

    call this%ctx%global_sum(this%nnode, this%nnode_tot)
    call this%ctx%global_sum(this%ncell, this%ncell_tot)
    call this%ctx%global_sum(merge(1, 0, this%ncell>0), this%npart) ! see NB above

    if (this%ncell > 0) then ! see NB above
      call h5_write_dataset(this%ctx, this%root_id, 'NumberOfPoints', this%nnode)
    else
      call h5_write_dataset(this%ctx, this%root_id, 'NumberOfPoints', idum)
    end if

    if (this%ncell > 0) then ! see NB above
      call h5_write_dataset(this%ctx, this%root_id, 'NumberOfCells',  this%ncell)
    else
      call h5_write_dataset(this%ctx, this%root_id, 'NumberOfCells',  idum)
    end if

    if (this%ncell > 0) then ! see NB above
      call h5_write_dataset(this%ctx, this%root_id, 'NumberOfConnectivityIds', size(cnode, kind=int64))
    else
      call h5_write_dataset(this%ctx, this%root_id, 'NumberOfConnectivityIds', idum)
    end if

    if (this%ncell > 0) then ! see NB above
      call h5_write_dataset(this%ctx, this%root_id, 'Offsets', xcnode-1) ! offsets instead of starting indices
    else
#ifdef GNU_PR123899
      block
        ${i["decl"]}$ :: idum(0)
        call h5_write_dataset(this%ctx, this%root_id, 'Offsets', idum)
      end block
#else
      call h5_write_dataset(this%ctx, this%root_id, 'Offsets', [${i["decl"]}$::])
#endif
    end if

#ifdef GNU_PR123899
    if (this%ncell > 0) then
      call h5_write_dataset(this%ctx, this%root_id, 'Connectivity', cnode-1)  ! 0-based indexing
    else
      block
        ${i["decl"]}$ :: idum(0)
        call h5_write_dataset(this%ctx, this%root_id, 'Connectivity', idum)
      end block
    endif
#else
    call h5_write_dataset(this%ctx, this%root_id, 'Connectivity', cnode-1)  ! 0-based indexing
#endif

    call h5_write_dataset(this%ctx, this%root_id, 'Types', types)
    call h5_write_dataset(this%ctx, this%root_id, 'Points', x)

  end subroutine write_mesh_${r["kind"]}$_${i["kind"]}$

#:endfor
#:endfor
  !! Writes the cell data ARRAY to a new CellData group dataset NAME. In the
  !! case of a temporal file supporting time-dependent datasets, this dataset
  !! is static and not associated with any time step. Note that VTK only
  !! supports scalar and vector-valued mesh data (rank-1 and 2 ARRAY); this
  !! should be a compile time constraint implemented by the public API.
#:for s in num_specs

  subroutine write_cell_data_${s["kind"]}$(this, name, array)
    class(vtkhdf_ug), intent(in) :: this
    character(*), intent(in) :: name
    ${s["decl"]}$, intent(in) :: array(..)
    integer, allocatable :: dims(:)
    dims = shape(array)
    INSIST(this%ctx%global_all(dims(size(dims)) == this%ncell))
    call h5_write_dataset(this%ctx, this%cgrp_id, name, array)
  end subroutine
#:endfor

  !! Writes the point data ARRAY to a new PointData group dataset NAME. In the
  !! case of a temporal file supporting time-dependent datasets, this dataset
  !! is static and not associated with any time step. Note that VTK only
  !! supports scalar and vector-valued mesh data (rank-1 and 2 ARRAY); this
  !! should be a compile time constraint implemented by the public API.
#:for s in num_specs

  subroutine write_point_data_${s["kind"]}$(this, name, array)
    class(vtkhdf_ug), intent(in) :: this
    character(*), intent(in) :: name
    ${s["decl"]}$, intent(in) :: array(..)
    integer, allocatable :: dims(:)
    dims = shape(array)
    INSIST(this%ctx%global_all(dims(size(dims)) == this%nnode))
    call h5_write_dataset(this%ctx, this%pgrp_id, name, array)
  end subroutine
#:endfor

  !! Register NAME as a time-dependent CellData group dataset. This writes
  !! no data, but configures a new extendable HDF5 dataset for future writing.
  !!
  !! The rank-1 array MOLD determines the dataset's fixed leading dimensions:
  !! - For scalar-valued point data: MOLD has size 0;
  !! - For vector-valued point data: MOLD size equals the number of components.
  !!
  !! The type and kind of MOLD defines the HDF5 datatype, and its value is
  !! ignored. The last dimension of written data will correspond to the cell
  !! index, making it the extendable (slowest-varying) dimension of the dataset.
  !! All MPI ranks must provide identical MOLD characteristics.

  !! Register NAME as a time-dependent CellData group dataset. This writes
  !! no data, but configures a new extendable HDF5 dataset for future writing.
  !!
  !! MOLD determines the dataset's fixed leading dimensions:
  !! - For scalar-valued cell data: MOLD is a scalar;
  !! - For vector-valued cell data: MOLD is a 1D array whose size equals the
  !!   number of components.
  !!
  !! The type and kind of MOLD defines the HDF5 datatype, and its value is
  !! ignored. The last dimension of written data will correspond to the cell
  !! index, making it the extendable (slowest-varying) dimension of the dataset.
  !! All MPI ranks must provide identical MOLD characteristics.
#:for s in num_specs

  subroutine register_temporal_cell_data_${s["kind"]}$(this, name, mold)

    class(vtkhdf_ug), intent(inout) :: this
    character(*), intent(in) :: name
    ${s["decl"]}$, intent(in) :: mold(..)

    type(temporal_data), pointer :: new

    INSIST(this%nsteps >= 0) ! must be configured for temporal
    INSIST(this%nsteps == 0) ! cannot register new dataset after time stepping has begun

    !! Dataset for the cell data
    associate (chunk_size => int(this%ncell_tot))
      call h5_create_unlimited_dataset(this%ctx, this%cgrp_id, name, mold, chunk_size)
    end associate

    !! Dataset for its time step offsets
    associate (mold => 0_int64, chunk_size => 100)
      call h5_create_unlimited_dataset(this%ctx, this%cogrp_id, name, mold, chunk_size)
    end associate

    !! Add to list CellData datasets
    allocate(new)
    new%name = name
    new%next => this%temporal_cell_data
    this%temporal_cell_data => new

  end subroutine register_temporal_cell_data_${s["kind"]}$
#:endfor

  !! Register NAME as a time-dependent PointData group dataset. This writes
  !! no data, but configures a new extendable HDF5 dataset for future writing.
  !!
  !! MOLD determines the dataset's fixed leading dimensions:
  !! - For scalar-valued point data: MOLD is a scalar;
  !! - For vector-valued point data: MOLD is a 1D array whose size equals the
  !!   number of components.
  !!
  !! The type and kind of MOLD defines the HDF5 datatype, and its value is
  !! ignored. The last dimension of written data will correspond to the point
  !! index, making it the extendable (slowest-varying) dimension of the dataset.
  !! All MPI ranks must provide identical MOLD characteristics.
#:for s in num_specs

  subroutine register_temporal_point_data_${s["kind"]}$(this, name, mold)

    class(vtkhdf_ug), intent(inout) :: this
    character(*), intent(in) :: name
    ${s["decl"]}$, intent(in) :: mold(..)

    type(temporal_data), pointer :: new

    INSIST(this%nsteps >= 0) ! must be configured for temporal
    INSIST(this%nsteps == 0) ! cannot register new dataset after time stepping has begun

    !! Dataset for the point data
    associate (chunk_size => int(this%nnode_tot))
      call h5_create_unlimited_dataset(this%ctx, this%pgrp_id, name, mold, chunk_size)
    end associate

    !! Dataset for its time step offsets
    associate (mold => 0_int64, chunk_size => 100)
      call h5_create_unlimited_dataset(this%ctx, this%pogrp_id, name, mold, chunk_size)
    end associate

    !! Add to list PointData datasets
    allocate(new)
    new%name = name
    new%next => this%temporal_point_data
    this%temporal_point_data => new

  end subroutine register_temporal_point_data_${s["kind"]}$
#:endfor

  !! Mark the start of a new time step with time value TIME. Subsequent output
  !! of time-dependent datasets will be associated with this time step.

  subroutine write_time_step(this, time)

    class(vtkhdf_ug), intent(inout) :: this
    real(real64), intent(in) :: time

    type(temporal_data), pointer :: tmp

    INSIST(this%nsteps >= 0)

    this%nsteps = this%nsteps + 1
    call h5_write_attr(this%ctx, this%steps_id, 'NSteps', this%nsteps)

    call h5_append_to_dataset(this%ctx, this%steps_id, 'Values', time, root=0)

    !! A single mesh is used for all time steps so there are no offsets
    call h5_append_to_dataset(this%ctx, this%steps_id, 'PointOffsets', 0_int64, root=0)
    call h5_append_to_dataset(this%ctx, this%steps_id, 'CellOffsets', 0_int64, root=0)
    call h5_append_to_dataset(this%ctx, this%steps_id, 'ConnectivityIdOffsets', 0_int64, root=0)
    call h5_append_to_dataset(this%ctx, this%steps_id, 'NumberOfParts', this%npart, root=0)
    call h5_append_to_dataset(this%ctx, this%steps_id, 'PartOffsets', 0_int64, root=0)

    !! Cell and point data offsets for each of the temporal datasets for this timestep

    tmp => this%temporal_point_data
    do while (associated(tmp))
      tmp%flag = .false.  ! dataset not yet written for this time step
      call h5_append_to_dataset(this%ctx, this%pogrp_id, tmp%name, tmp%next_offset, root=0)
      tmp => tmp%next
    end do

    tmp => this%temporal_cell_data
    do while (associated(tmp))
      tmp%flag = .false.  ! dataset not yet written for this time step
      call h5_append_to_dataset(this%ctx, this%cogrp_id, tmp%name, tmp%next_offset, root=0)
      tmp => tmp%next
    end do

  end subroutine write_time_step

  !! Write the cell-based ARRAY to the time-dependent CellData group dataset
  !! NAME. The data is associated with the current time step. The type and
  !! leading dimensions of ARRAY must match the MOLD provided during the
  !! registration of NAME. The last (or only) dimension of ARRAY must
  !  correspond to the local number of cells on this MPI rank.
#:for s in num_specs

  subroutine write_temporal_cell_data_${s["kind"]}$(this, name, array)

    class(vtkhdf_ug), intent(in) :: this
    character(*), intent(in) :: name
    ${s["decl"]}$, intent(in) :: array(..)

    integer, allocatable :: dims(:)
    type(temporal_data), pointer :: dset

    INSIST(this%nsteps > 0)

    dims = shape(array)
    INSIST(size(dims) >= 1 .and. size(dims) <= 3)
    INSIST(dims(size(dims)) == this%ncell)

    dset => this%temporal_cell_data
    do while (associated(dset))
      if (dset%name == name) exit
      dset => dset%next
    end do
    INSIST(associated(dset))

    INSIST(.not.dset%flag) ! no double writing in a single step
    call h5_append_to_dataset(this%ctx, this%cgrp_id, name, array)
    dset%next_offset = dset%next_offset + this%ncell_tot
    dset%flag = .true. ! dataset has been written for this step

  end subroutine write_temporal_cell_data_${s["kind"]}$
#:endfor

  !! Write the point-based ARRAY to the time-dependent PointData group dataset
  !! NAME. The data is associated with the current time step. The type and
  !! leading dimensions of ARRAY must match the MOLD provided during the
  !! registration of NAME. The last (or only) dimension of ARRAY must
  !  correspond to the local number of points on this MPI rank.
#:for s in num_specs

  subroutine write_temporal_point_data_${s["kind"]}$(this, name, array)

    class(vtkhdf_ug), intent(in) :: this
    character(*), intent(in) :: name
    ${s["decl"]}$, intent(in) :: array(..)

    integer, allocatable :: dims(:)
    type(temporal_data), pointer :: dset

    INSIST(this%nsteps > 0)

    dims = shape(array)
    INSIST(size(dims) >= 1 .and. size(dims) <= 3)
    INSIST(dims(size(dims)) == this%nnode)

    dset => this%temporal_point_data
    do while (associated(dset))
      if (dset%name == name) exit
      dset => dset%next
    end do
    INSIST(associated(dset))

    INSIST(.not.dset%flag) ! no double writing in a single step
    call h5_append_to_dataset(this%ctx, this%pgrp_id, name, array)
    dset%next_offset = dset%next_offset + this%nnode_tot
    dset%flag = .true. ! dataset has been written for this step

  end subroutine write_temporal_point_data_${s["kind"]}$
#:endfor

end module vtkhdf_ug_type
